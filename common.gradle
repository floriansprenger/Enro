ext.publishModule = { String groupName, String moduleName ->
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    def privateProperties = new Properties()
    privateProperties.load(new FileInputStream(rootProject.file("private.properties")))

    def versionProperties = new Properties()
    versionProperties.load(new FileInputStream(rootProject.file("version.properties")))

    bintray {
        user = privateProperties["bintrayUser"]
        key = privateProperties["bintrayToken"]
        publications = ['release']
        pkg {
            repo = "Enro"
            name = moduleName
            licenses = ["Apache-2.0"]
            vcsUrl = "https://github.com/isaac-udy/Enro.git"

            version {
                name = versionProperties["versionName"]
            }
        }
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId groupName
                artifactId moduleName
                version versionProperties["versionName"]

                artifact androidSourcesJar
                artifact("$buildDir/outputs/aar/$moduleName-release.aar")

                pom.withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/isaac-udy/Enro")
                credentials {
                    username = privateProperties['githubUser']
                    password = privateProperties['githubToken']
                }
            }
        }
    }
    tasks.publish.dependsOn(tasks.assemble)
}